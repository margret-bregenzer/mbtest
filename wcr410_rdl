%*** WCR410.RDL
%***
%*** Revision History ***
%*** 2-feb-2005 PDurland remove calvert specific title
%*** 20-AUG-93 BBEACOM   Corrected WHERE clause for
%*** .................... retrieval; used MSSDAT to
%*** .................... get reversed dates;
%*** 21-Jul-93 BBEACOM    Initial version
%*** Margret
xxxxxx
test
%***
%PROGRAM-ID
       WCR410
%AUTHOR
       PKS-IS/KMG
%NARRATIVE
      **  Equipment Availability by Month and Period
%FILES
        MFILE = MSF410A
        AFILE = MSF600
        LFILE = MSF600
        RFILE = WCR410A

%ROUTINES
 MSSDAT
 MSSCNV

%SELECTION
 WHERE (DSTRCT-CODE-2 = REQ-DIST, -
       REV-STAT-DATE-2 >= EDATE2, -
       REV-STAT-DATE-2 <= SDATE1)
       SELECTIF (AVAIL-CODE = 'OPHR')
       SELECTIF (AVAIL-CODE = 'TREP')

%ORDER
 P
 DSPEQ

%BREAK-FIELDS
 PARENT-EQUIP VIA (PX)
 DSPEQ

%REPORT-TITLE
'EQUIPMENT AVAILABILITY FOR MONTH AND PERIOD-TO-DATE'
%PAGE HEADING
<IF END-PER <> BEG-PER>
'% AV = OPERATE / (OPERATE + REPAIR)'
'                  <--------'{MNAME   }'--------->                 <----' {  P1} 'TO' {  P2} '----->'
'EQUIP #           OPERATE    REPAIR      % AV                     OPERATE    REPAIR        % AV'
<ELSE>
'% AV = OPERATE / (OPERATE + REPAIR)'
'                  <--------'{MNAME   }'--------->'
'EQUIP #           OPERATE    REPAIR      % AV'
<ENDIF>
%BREAK PARENT-EQUIP VIA(PX) HEADING
%*
%* P = PARENT-EQUIP
%* PUDESC = ITEM-NAME-1 FOR PARENT-EQUIP

 PPPPPPPPPPPP {PUDESC                            }

%BREAK DSPEQ FOOTING
<IF END-PER <> BEG-PER>
 {DSPEQ     }    {   TOPHR}{   TTREP}    {    AVL}                  {    PTOPHR}  { PTTREP}     {      PAV}
<ELSE>
 {DSPEQ     }    {   TOPHR}{   TTREP}    {    AVL} 
<ENDIF>
%BREAK PARENT-EQUIP VIA(PX) FOOTING
<IF END-PER <> BEG-PER>
'============    =================================                  ======================================='
 PPPPPPPPPPPP    {   TOPHR}{   TTREP}    {    AVL}                  {    PTOPHR}  { PTTREP}     {      PAV}
<ELSE>
'============    ================================='
 PPPPPPPPPPPP    {   TOPHR}{   TTREP}    {    AVL}                 
<ENDIF>
%DETAIL
%*
%* THE AVAILABLITY STATISTICS FILE HAS MULTIPLE ROWS FOR EACH EQUIPMENT WITHIN A PERIOD
%* SINCE THIS IS REPORTING CUMULATIVE STATISTICS, THERE IS NO DETAIL LINE, ONLY CONTROL
%* BREAKS ON EQUIP-NO TO REPORT THE TOTALS FOR THE EQUIPMENT, AND PRODUCTIVE UNIT NO?
%REPORT-SUMMARY
%*

 '* * * END OF AVAILABILITY REPORT * * *'
%CONTROL

%REQUEST-PARAMETERS
 DCODE LIKE DSTRCT-CODE TEXT 'Selected District: '
 BPER PIC (X(4))  TEXT 'Beginning Period:  '
 EPER PIC (X(4))  TEXT 'Ending Period:     '

%VARIABLES
%*
%* derive the district from user input
%* use users' input if not blank else
%* use current district
REQ-DIST = CHOOSE (%DCODE% IF (%DCODE% <> SPACES), -
     %DISTRICT-CODE%) I-PIC (X(4)) CONSTANT
%*
%* GET DATES FROM PERIODS ENTERED BY THE USER
%* DATES ARE USED IN SELECTION AND IN FILTERING CALCULATIONS BASED ON THE
%* STATISTIC DATE IN THE MSF410 FILE
%*
%* SDATE1 = THE FIRST ACCOUNTING DAY IN THE USER-SPECIFIED BEGINNING PERIOD (REVERSED)
%* EDATE2 = THE LAST  ACCOUNTING DAY IN THE USER-SPECIFIED ENDING    PERIOD (REVERSED)
%* SDATE2 = THE FIRST ACCOUNTING DAY IN THE USER-SPEICIFED ENDING    PERIOD (REVERSED)
%* 
%* derive the current period if user defaulted to spaces
CP = GET (CURRENT-PERIOD FROM ('MSS001')) CONSTANT
END-PER = CHOOSE (%EPER% IF (%EPER% <> SPACES), CP) I-PIC (X(4)) CONSTANT
%* derive the first period of the year if the user defaulted
YY = SUBSTR (END-PER, 1, 2) CONSTANT
FPER = CATENATE (YY,'01') CONSTANT
BEG-PER = CHOOSE (%BPER% IF (%BPER% <> SPACES), FPER) CONSTANT
SDATE1 = STARTING-DATE-1-9 OF MSSDAT WHERE (OPTION = 'H',  -
         REV-FORMAT = '2', -
      PERIOD-YRMN = BEG-PER) CONSTANT
SDATE2 = STARTING-DATE-1-9 OF MSSDAT WHERE (OPTION = 'H', -
         REV-FORMAT = '2', -
      PERIOD-YRMN = END-PER) CONSTANT
EDATE2 = ENDING-DATE-1-9 VIA (SDATE2) CONSTANT
SPER-SDATE-R = SDATE1 FORMAT (REVERSE-DATE) CONSTANT
EPER-SDATE-R = SDATE2 FORMAT (REVERSE-DATE) CONSTANT
EPER-EDATE-R = EDATE2 FORMAT (REVERSE-DATE) CONSTANT
P1 = BEG-PER FORMAT (PERIOD) CONSTANT
P2 = END-PER FORMAT (PERIOD) CONSTANT
SMON = MONTH-NAME OF MSSDAT WHERE (OPTION = 'A', -
       REV-FORMAT = '1', -
       DATE1 = EDATE2) CONSTANT
ALTMON = CATENATE ('Month ',SMON) CONSTANT
%*MNAME = '   June  ' CONSTANT
MNAME = CHOOSE (' January ' IF (SMON = 'JAN'), -
              'February ' IF (SMON = 'FEB'), -
  '   March ' IF (SMON = 'MAR'), -
  '   April ' IF (SMON = 'APR'), -
  '    May  ' IF (SMON = 'MAY'), -
  '   June  ' IF (SMON = 'JUN'), -
  '   July  ' IF (SMON = 'JUL'), -
  '  August ' IF (SMON = 'AUG'), -
  'September' IF (SMON = 'SEP'), -
  ' October ' IF (SMON = 'OCT'), -
  'November ' IF (SMON = 'NOV'), -
  'December ' IF (SMON = 'DEC'), -
  ALTMON) CONSTANT
%*
%* TOPHR = TOTAL OPERATING HOURS IN THE CURRENT MONTH FOR A PIECE OF EQUIPMENT
%* TTREP = TOTAL REPAIR    HOURS IN THE CURRENT MONTH FOR A PIECE OF EQUIPMENT
%* AVL   = PERCENT AVAILABLE     IN THE CURRENT MONTH FOR A PIECE OF EQUIPMENT
%* PTOPHR = TOTAL OPERATING HOURS FOR THE SELECTED PERIOD RANGE FOR A PIECE OF EQUIPMENT
%* PTTREP = TOTAL REPAIR    HOURS FOR THE SELECTED PERIOD RANGE FOR A PIECE OF EQUIPMENT
%* PAV   = PERCENT AVAILABLE      FOR THE SELECTED PERIOD RANGE FOR A PIECE OF EQUIPMENT

%*  MUST FURTHER RESTRICT MONTH STATS BECAUSE ONLY PART OF THE PERIOD
OPHR = AVAIL-HOURS IF (AVAIL-CODE = 'OPHR') ELSE (0)
SOPHR = CHOOSE (OPHR IF (REV-STAT-DATE >= EDATE2, REV-STAT-DATE <= SDATE2), 0)
TREP = AVAIL-HOURS IF (AVAIL-CODE = 'TREP') ELSE (0)
STREP = CHOOSE (TREP IF (REV-STAT-DATE >= EDATE2, REV-STAT-DATE <= SDATE2), 0)
%* CURRENT MONTH (OR SPECIFIED END-PERIOD MONTH) TOTALS
TOPHR = TOTAL (SOPHR) D-PIC (Z(6)9.9)
TTREP = TOTAL (STREP) D-PIC (Z(7)9.9)
%* ENTIRE PERIOD TOTALS (ASSUMES WHERE CLAUSE THREW OUT RECORDS WITH DATE NOT IN RANGE)
PTOPHR = TOTAL (OPHR) D-PIC (Z(7)9.9)
PTTREP = TOTAL (TREP) D-PIC (Z(7)9.9)
%* CURRENT MONTH AVAILABLE % COMPUTATION
XAVL = CALC (TOPHR * 100 / (TOPHR + TTREP)) D-PIC (ZZZ9.9)
%* PERIOD AVAILABLE PERCENT CALCULATION
PAV = CALC (PTOPHR * 100 / (PTOPHR + PTTREP)) D-PIC (ZZZ9.9)
TDIV = CALC(TOPHR + TTREP)
AVL = CHOOSE(XAVL IF (TDIV <> 0), 0) D-PIC (ZZZ9.9)
%*
%*  GET PRODUCTIVE UNIT ID AND DESCRIPTION
%*
E = EQUIP-NO-2
PX = PARENT-EQUIP OF MSF600 WHERE (EQUIP-NO = E)
PLANT = PLANT-NO VIA (PX)
DSPEQ = CHOOSE(PLANT IF(PLANT <> SPACES), E)
P = SUBSTR (PX,1,12)
PUDESC1 = ITEM-NAME-1 VIA (PX)
PUDESC = OUT-STRING OF MSSCNV WHERE (OPTION = 'P', INP-STRING=PUDESC1)
